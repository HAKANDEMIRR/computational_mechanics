finish
/clear

/input, ../config.py

! Video and color settings
    /RGB,INDEX,100,100,100, 0
    /RGB,INDEX, 80, 80, 80,13
    /RGB,INDEX, 60, 60, 60,14
    /RGB,INDEX, 0, 0, 0,15
    /type,,4
    /efacet,4
    /graph,POWER

! Defining arrays
    *dim, freq1_2d,   array,elsize_num
    *dim, freq2_2d,   array,elsize_num
    *dim, freq3_2d,   array,elsize_num
    *dim, freq1_3d,   array,elsize_num
    *dim, freq2_3d,   array,elsize_num
    *dim, freq3_3d,   array,elsize_num
    *dim, freq1_beam, array,elsize_num
    *dim, freq2_beam, array,elsize_num
    *dim, freq3_beam, array,elsize_num
    *dim, dof_2d,     array,elsize_num
    *dim, dof_3d,     array,elsize_num
    *dim, dof_beam,   array,elsize_num

! Calculating for each elsize
*do,i,1,elsize_num

    /prep7
    elsize = elsize_start/i

    finish


    ! 2D problem
        /filname, model2d
        /title, 2D solution
        /prep7

        ! Material definition
            mp,ex,  1, E
            mp,dens,1, rho
            mp,nuxy,1, nu

        ! Elemet type definition
            et,1,et_2d,,,3
            r,1, h
            real,1

        ! Geometry
            pcirc,R-b/2,R+b/2,3*phi/2,phi/2

        ! Boundary conditions
            csys,1
            lsel,s,loc,y,3*phi/2
            lsel,a,loc,y,phi/2
            dl,all,,all,0
            alls

        ! Meshing
            esize,elsize
            amesh,all
            alls
            *get,dof_2d(i),NODE,0,COUNT
            finish

        ! Solution
            /solu
            outres,all,all
            antype,modal
            mxpand,3
            modopt,lanb,3,,,
            solve
            finish

        ! Postprocessing
            /post1
            *get,freq1_2d(i),mode,1,freq
            *get,freq2_2d(i),mode,2,freq
            *get,freq3_2d(i),mode,3,freq
            *if,i,EQ,elsize_num,THEN
                ! /expand,2,RECT,HALF,0.00001
                /show,png
                /erase
                /win,1,ltop $/win,2,rtop
                /win,3,lbot 
                /view,all,,,1
                /angle,all,0
                /win,2,off $/win,3,off
                /plopt,info,0
                /annot,delete
                /tspec, 15,0.700,1,0,0
                *dim,anx,,3
                *dim,any,,3
                *dim,freq_2d,,3
                anx(1) = -.98, .4, -.98
                any(1) = .01, .01, -.92
                *do,k,1,3
                    set,1,k
                    pldisp,2
                    /win,k,off
                    /win,k+1,on
                    /noerase
                *enddo
                /tlab,anx(1),any(1),Mode 1: %freq1_2d(i)% Hz
                /tlab,anx(2),any(2),Mode 2: %freq2_2d(i)% Hz
                /tlab,anx(3),any(3),Mode 3: %freq3_2d(i)% Hz
                /replot
                /erase
                /show,close
                any=
                anx=
            *endif    
            finish

    finish
    PARSAV,ALL,PARAM_FILE,TXT
    /clear,NOSTART
    PARRES,NEW,PARAM_FILE,TXT  

    ! 3D problem
        /filname, model3d
        /title, 3D solution
        /prep7

        ! Material definition
            mp,ex,  1, E
            mp,dens,1, rho
            mp,nuxy,1, nu

        ! Elemet type definition
            et,1,et_3d

        ! Geometry
            cylind,R-b/2,R+b/2,-b/2,b/2,3*phi/2,phi/2

        ! Boundary conditions
            csys,1
            asel,s,loc,y,3*phi/2
            asel,a,loc,y,phi/2
            da,all,all,0
            asel,all
            da,all,uz,0
            alls

        ! Meshing
            esize,elsize
            vmesh,all
            alls
            *get,dof_3d(i),NODE,0,COUNT
            finish

        ! Solution
            /solu
            outres,all,all
            antype,modal
            mxpand,3
            modopt,lanb,3,,,
            solve
            finish

        ! Postprocessing
            /post1
            *get,freq1_3d(i),mode,1,freq
            *get,freq2_3d(i),mode,2,freq
            *get,freq3_3d(i),mode,3,freq
            *if,i,EQ,elsize_num,THEN
                ! /expand,2,RECT,HALF,0.00001
                /show,png
                /erase
                /win,1,ltop $/win,2,rtop
                /win,3,lbot 
                /view,all,,,1
                /angle,all,0
                /win,2,off $/win,3,off
                /plopt,info,0
                /annot,delete
                /tspec, 15,0.700,1,0,0
                *dim,anx,,3
                *dim,any,,3
                anx(1) = -.98, .4, -.98
                any(1) = .01, .01, -.92
                *do,k,1,3
                    set,1,k
                    pldisp,2
                    /win,k,off
                    /win,k+1,on
                    /noerase
                *enddo
                /tlab,anx(1),any(1),Mode 1: %freq1_3d(i)% Hz
                /tlab,anx(2),any(2),Mode 2: %freq2_3d(i)% Hz
                /tlab,anx(3),any(3),Mode 3: %freq3_3d(i)% Hz
                /replot
                /erase
                /show,close
                any=
                anx=
            *endif    
            finish

    finish
    PARSAV,ALL,PARAM_FILE,TXT
    /clear,NOSTART
    PARRES,NEW,PARAM_FILE,TXT  

    ! Beam problem
        /filname, modelbeam
        /title, Beam solution
        /prep7

        ! Material definition
            mp,ex,  1, E
            mp,dens,1, rho
            mp,nuxy,1, nu

        ! Elemet type definition
            et,1,et_beam
            sectype,1,BEAM,rect
            secdata,b,h

        ! Geometry
            csys,1
            k,1,R,phi+phi/2,0
            k,2,R,phi/2,0
            k,3,0,0,0
            larc,1,2,3,R

        ! Boundary conditions
            ksel,s,loc,y,phi+phi/2
            ksel,a,loc,y,phi/2
            dk,all,all,0
            lsel,all
            dl,all,,uz,0
            alls

        ! Meshing
            esize,elsize
            lmesh,all
            alls
            *get,dof_beam(i),NODE,0,COUNT
            alls
            finish

        ! Solution
            /solu
            outres,all,all
            antype,modal
            mxpand,3
            modopt,lanb,3,,,
            solve
            finish

        ! Postprocessing
            /post1
            *get,freq1_beam(i),mode,1,freq
            *get,freq2_beam(i),mode,2,freq
            *get,freq3_beam(i),mode,3,freq
            *if,i,EQ,elsize_num,THEN
                ! /expand,2,POLAR,HALF,,270
                /show,png
                /erase
                /win,1,ltop $/win,2,rtop
                /win,3,lbot 
                /view,all,,,1
                /angle,all,0
                /win,2,off $/win,3,off
                /plopt,info,0
                /annot,delete
                /tspec, 15,0.700,1,0,0
                *dim,anx,,3
                *dim,any,,3
                anx(1) = -.98, .4, -.98
                any(1) = .01, .01, -.92
                *do,k,1,3
                    set,1,k
                    ! /dscale,1,-0.06
                    pldisp,2
                    /win,k,off
                    /win,k+1,on
                    /noerase
                *enddo
                /tlab,anx(1),any(1),Mode 1: %freq1_beam(i)% Hz
                /tlab,anx(2),any(2),Mode 2: %freq2_beam(i)% Hz
                /tlab,anx(3),any(3),Mode 3: %freq3_beam(i)% Hz
                /replot
                /erase
                /show,close
                any=
                anx=
            *endif    
            finish
    
    finish
    PARSAV,ALL,PARAM_FILE,TXT
    /clear,NOSTART
    PARRES,NEW,PARAM_FILE,TXT

*enddo

! Export results

    /POST1
    *CFOPEN,result_data,TXT
    *VWRITE,dof_2d(1),freq1_2d(1),freq2_2d(1),freq3_2d(1)
    (F12.5,' ',F12.5,' ',F12.5,' ',F12.5)

    *VWRITE,'  '
    (A,' ')

    *VWRITE,dof_3d(1),freq1_3d(1),freq2_3d(1),freq3_3d(1)
    (F12.5,' ',F12.5,' ',F12.5,' ',F12.5)

    *VWRITE,'  '
    (A,' ')

    *VWRITE,dof_beam(1),freq1_beam(1),freq2_beam(1),freq3_beam(1)
    (F12.5,' ',F12.5,' ',F12.5,' ',F12.5)
    *CFCLOS

    /sys,copy *.png ..\results