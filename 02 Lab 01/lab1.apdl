finish
/clear

/filname,model
/TITLE, Lab 1. Full model 
/input, ../params.txt
/input, ../config.py
dd = D/dcoef
r = k*dd
/prep7

ET,1,eltype1,,,1
MP,ex,1,e
MP,nuxy,1,nu
esize,es0

K,101,0,0
K,102,0,D_inf+r
K,103,dd/2,D_inf+r

K,106,D/2
K,107,dd/2+r,D_inf+r
l,101,102
l,102,103

*if,dd/2+r,GE,D/2,THEN
    K,104,dd/2+r,D_inf
    larc,103,104,107,r
    K,108,D/2,D_inf+r
    l,108,106        
    l,106,101
    lovlap,3,4
    *if,dd/2+r,EQ,D/2,THEN
        a,101,102,103,104,106
    *else
        a,101,102,103,1,106
    *endif    
    sfl,5,pres,-sigma 
*else
    K,104,dd/2+r,D_inf
    K,105,D/2,D_inf
    larc,103,104,107,r
    l,104,105
    l,105,106
    l,106,101
    a,101,102,103,104,105,106
    sfl,6,pres,-sigma    
*endif

hptcreate, area,1,,coord,dd/2+r/2,D_inf-dd/3.9,0

dl,2,,UY,0
dl,1,,UX,0

numstr,node,1000
amesh,all

/solu
anty,static
outres,all,all
solve
finish

/post1
nsel,s,loc,y,D_inf+r-10e-6,D_inf+r+10e-6
nsel,r,loc,x,dd/2-10e-6,dd/2+10e-6
/HEADER, OFF
/OUTPUT,stress_model.txt
PRNSOL,S,COMP
/OUTPUT
nsel,all
/OUTPUT,dof_model.txt
*GET,dofnum,NODE,0,COUNT
/OUTPUT
conv_node = node(dd/2+r/2,D_inf-dd/3.9,0)
/OUTPUT,disp_conv.txt
*GET,dispUY,NODE,conv_node,U,SUM
/OUTPUT
finish
save

*if,submodel,EQ,1,THEN
    finish
    /clear, nostart
    /filname, submodel
    /TITLE, Lab 1. Submodel 
    /input, ../params.txt
    /input, ../config.py
    dd = D/dcoef
    r = k*dd
    /prep7
    ET,1,eltype2,,,1
    MP,ex,1,e
    MP,nuxy,1,nu
    esize,es1
    K,103,dd/2,D_inf+r
    K,104,dd/2+r,D_inf
    K,107,dd/2+r,D_inf+r
    larc,103,104,107,r
    K,201,dd/4,D_inf+r
    K,202,dd/2+r,D_inf-dd/4
    larc,201,202,103,r+dd/4
    l,202,104
    l,201,103
    a,201,202,104,103
    esize,es1/4
    amap,1,201,202,104,103
    lsel,s,,,2
    lsel,a,,,3
    nsll,s,1
    nwrite !saves nodes coords to submodel.node
    finish
    save

    /post1
    resume,model,db
    file,model,rst
    cbdof,,,,,,,0,,0 !get nodes deform to submodel.cbdo
    finish

    /prep7
    resume
    nsel,all
    /NOPR
    /input,,cbdo,,:cb1
    /GOPR
    nsel,s,loc,y,D_inf+r-10e-6,D_inf+r+10e-6
    d,all,UY,0
    nsel,all
    finish

    /solu
    solve
    finish

    /post1
    nsel,s,loc,y,D_inf+r-10e-6,D_inf+r+10e-6
    nsel,r,loc,x,dd/2-10e-6,dd/2+10e-6
    /HEADER, OFF
    /OUTPUT,stress_submodel.txt
    PRNSOL,S,COMP
    /OUTPUT
    nsel,all
    /OUTPUT,dof_submodel.txt
    *GET,dofnum,NODE,0,COUNT
    /OUTPUT
    nsel,all
    finish
*endif