finish
/clear

/input, ../config.py

! Video and color settings
    /RGB,INDEX,100,100,100, 0
    /RGB,INDEX, 80, 80, 80,13
    /RGB,INDEX, 60, 60, 60,14
    /RGB,INDEX, 0, 0, 0,15
    /graph,POWER

! Defining arrays
    *dim, grad1,array,pathpts+1
    *dim, grad2,array,pathpts+1
    *dim, dof1, array,elsize_num
    *dim, dof2, array,elsize_num
    *dim, gmax1, array,elsize_num
    *dim, gmax2, array,elsize_num
    *dim, tmax1, array,elsize_num
    *dim, tmax2, array,elsize_num

! Calculating for each elsize
*do,i,1,elsize_num

    elsize = elsize_start1/i

    ! Task 2.1
        /filname, task2_1
        /title, Task 2.1
        /prep7

        ! Material definition
            mp,kxx,1, k

        ! Elemet type definition
            et,1,et1

        ! Geometry
            pcirc,0,b1,-90,90
            cyl4,0,b1,0,,a1
            asba,1,2

        ! Meshing
            esize,elsize
            amesh,all
            alls
            *get,dof1(i),node,0,count

        ! Boundary conditions
            lsel,s,,,9
            lsel,a,,,3
            dl,all,,symm
            lsel,s,,,10
            lsel,a,,,8
            dl,all,,temp,0
            alls
            bfa,all,hgen,q
            alls
            finish

        ! Solution
            /solu
            ncnv,,1.0E10
            antype,static
            outres,all,all
            solve
            finish

        ! Postprocessing
            /post1

            N1 = NODE(0,b1-a1,0)
            N2 = NODE(0,-b1,0)
            path, T_Grad, 2,2,pathpts
            ppath, 1, N1
            ppath, 2, N2
            pdef, T_Grad,TG,sum
            pdef, T_Path,temp
            *get,gmax1(i),path,0,max,T_Grad
            *get,tmax1(i),path,0,max,T_Path

            *if,i,EQ,elsize_num,then
                *do,k,1,pathpts+1
                    *get,grad1(k),path,0,item,T_Grad,PATHPT,k
                *enddo
                /expand,2,rect,half,0.00001
                /erase                
                /show,png
                /win,1,ltop
                /win,2,rtop
                /win,3,bot
                /view,all,,,1
                /angle,all,0
                /win,2,off
                /win,3,off
                /plopt,info,0
                /annot,delete
                /tspec, 15,0.700,1,0,0

                pldisp,2
                plnsol,temp
                /win,1,off
                /win,2,on
                /noerase
                plnsol,tg,sum
                /win,2,off
                /win,3,on
                /noerase
                plpath,T_Grad
                /noerase

                /tlab,-0.90, 0.01,Temperature, K
                /tlab,  0.4, 0.01,Thermal Gradient, K/m
                /tlab,-0.05,-0.10,Thermal Gradient Diagram, K/m
                /replot
                /erase
                /show,close

            *endif

            padele,all

    finish
    PARSAV,ALL,PARAM_FILE,TXT
    /clear,NOSTART
    PARRES,NEW,PARAM_FILE,TXT  

    elsize = elsize_start2/i

    ! Task 2.2
        /filname, task2_2
        /title, Task 2.2
        /prep7

        ! Material definition
            mp,kxx,1, k

        ! Elemet type definition
            et,1,et2

        ! Geometry
            blc4,0,0,b2/2,a2
            blc4,0,d2,b2/2-d2,a2-d2
            asba,1,2

        ! Meshing
            esize,elsize
            hptcreate, area,3,,coord,b2/2-d2/2,d2/2,0
            amesh,all
            alls
            *get,dof2(i),node,0,count

        ! Boundary conditions
            lsel,s,,,9
            dl,all,,symm
            lsel,inve
            dl,all,,temp,0
            alls
            bfa,all,hgen,q
            alls
            finish

        ! Solution
            /solu
            ncnv,,1.0E10
            antype,static
            outres,all,all
            solve
            finish

        ! Postprocessing
            /post1

            N1 = NODE(0,0,0)
            N2 = NODE(b2/2-d2/2,d2/2,0)
            *get,gmax2(i),node,N1,TG,sum
            *get,tmax2(i),node,N2,Temp

            *if,i,EQ,elsize_num-1,then
                *dim, nodetemp, array,dof2(i)
                *do,k,1,dof2(i)
                    nsel,s,,,k
                    *get,nodetemp(k),node,k,temp
                *enddo
            *endif

            *if,i,EQ,elsize_num,then

                /expand,2,rect,half,0.00001
                /erase                
                
                /win,1,top
                /win,2,bot

                /view,all,,,1
                /angle,all,0
                /win,2,off
                /plopt,info,0
                /annot,delete
                /tspec, 15,0.700,1,0,0
                /show,png
                pldisp,2
                plnsol,temp
                /win,1,off
                /win,2,on
                /noerase
                plnsol,tg,sum
                /noerase
                /tlab,  0.2,  0.90,Temperature, K
                /tlab,  0.1, -0.10,Thermal Gradient, K/m
                /replot
                /erase
                /show,close

            *endif

        finish
        PARSAV,ALL,PARAM_FILE,TXT
        /clear,NOSTART
        PARRES,NEW,PARAM_FILE,TXT  
*enddo

! Export results
    /POST1
    *CFOPEN,result_data,TXT
    *VWRITE,dof1(1), tmax1(1), gmax1(1)
    (F15.3,' ',F15.3,' ',F15.3)

    *VWRITE,'  '
    (A,' ')

    *VWRITE,dof2(1), tmax2(1), gmax2(1)
    (F15.3,' ',F15.3,' ',F15.3)

    ! *VWRITE,'  '
    ! (A,' ')

    ! *VWRITE,dof_beam(1),freq1_beam(1),freq2_beam(1),freq3_beam(1)
    ! (F12.5,' ',F12.5,' ',F12.5,' ',F12.5)
    *CFCLOS

    ! /sys,copy *.png ..\results

    *CFOPEN,nodal_data,TXT
    *VWRITE,nodetemp(1)
    (F15.3,' ')
    *CFCLOS